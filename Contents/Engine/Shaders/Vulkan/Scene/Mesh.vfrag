#version 460

#ifndef VULKAN
    #define VULKAN
#endif

#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_ARB_shading_language_include : require

#include "../../Common/Shadow.glsl"

layout(location = 0) out vec4 outColor;

layout(set = 0, binding = 3) uniform sampler2D shadowMap;

layout(set = 1, binding = 0) uniform sampler2D textures[];

layout(location = 0) in Data {
    flat int textureIndex;
    vec2 UV;
    vec3 normal;
    vec4 color;
    vec3 meshPosition; 
    vec4 shadowPosition; 
    vec3 lightPosition; 
    vec3 cameraPosition; 
} inData;

void main() {
    vec3 color = texture(textures[inData.textureIndex], inData.UV).rgb;

    vec3 lightDirection   = normalize(inData.lightPosition - inData.meshPosition);
    vec3 viewDirection    = normalize(inData.cameraPosition - inData.meshPosition);
    vec3 reflectDirection = reflect(-lightDirection, inData.normal);
    vec3 halfwayDirection = normalize(lightDirection + viewDirection);

    vec3 diffuse  = max(dot(lightDirection, inData.normal), 0.0) * LIGHT_COLOR;
    vec3 specular = pow(max(dot(inData.normal, halfwayDirection), 0.0), 64.0) * LIGHT_COLOR;

    float shadow = ShadowCalculation(shadowMap, inData.shadowPosition, inData.normal, lightDirection);

    vec3 lighting = (AMBIENT_LIGHT + (1.0 - shadow) * (diffuse + specular)) * color;

    outColor = vec4(lighting, 1.0);
}