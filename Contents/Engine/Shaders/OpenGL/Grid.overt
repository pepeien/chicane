#version 460

#extension GL_ARB_shader_draw_parameters : require

struct Instance {
    vec2 screen;
    vec2 size;
    vec3 position;
};

layout(location = 1) out vec4 outColor;

layout(location = 0) in vec3 inPosition;
layout(location = 1) in vec4 inColor;
layout(location = 2) in vec2 inUV;
layout(location = 3) in vec3 inNormal;

layout(std430, binding = 3) buffer GridBuffer {
    Instance data[];
} instances;

void main() {
    Instance instance = instances.data[gl_InstanceID + gl_BaseInstance];

    vec2 vertex = inPosition.xy;
    vertex *= instance.size;   // Scale vertex to UI size
    vertex /= instance.screen; // Normalize to screen size

    vec3 position = instance.position;
    position.xy += instance.size * 0.5; // Account for component offset
    position.xy /= instance.screen;     // Normalize to screen size
    position.xy *= 2.0;                 // Normalize to NDC
    position.xy -= 1.0;                 // Normalize to NDC
    position.y  *= -1.0;                // Flips Y
    position.z  /= 999.9;
    position.z   = clamp(abs(position.z - 1.0), 0.0, 1.0);

    outColor = inColor / 255.0;

    gl_Position  = vec4(vertex + position.xy, position.z, 1.0);
}