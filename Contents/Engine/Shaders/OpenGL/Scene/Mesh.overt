#version 460

#extension GL_ARB_shading_language_include : require
#extension GL_ARB_shader_draw_parameters : require

#include "../../Common/Constant.glsl"
#include "../../Common/Poly/3D.glsl"

layout(binding = 0) uniform Camera {
    vec4 clip;

    mat4 view;
    mat4 projection;

    vec4 up;
    vec4 right;
    vec4 forward;

    vec4 translation;
} camera;

layout(binding = 1) uniform Light {
    vec4 clip;

    mat4 view;
    mat4 projection;

    vec4 up;
    vec4 right;
    vec4 forward;

    vec4 translation;
} light;

layout(std430, binding = 2) readonly buffer Scene {
    PolyInstance3D meshes[];
} instance;

layout(location = 0) out Data {
    flat int textureIndex;
    vec2 UV;
    vec3 normal;
    vec4 color;
    vec3 meshPosition;
    vec4 shadowPosition;
    vec3 lightPosition;
    vec3 cameraPosition;
} outData;

layout(location = 0) in vec3 inPosition;
layout(location = 1) in vec4 inColor;
layout(location = 2) in vec2 inUV;
layout(location = 3) in vec3 inNormal;

void main() {
    PolyInstance3D mesh = instance.meshes[gl_InstanceID + gl_BaseInstance];

    vec4 position = vec4(inPosition, 1.0);

    outData.textureIndex   = mesh.texture;
    outData.UV             = inUV;
    outData.normal         = transpose(mat3(mesh.model)) * inNormal;
    outData.color          = inColor;
    outData.meshPosition   = vec3(mesh.model * position);
    outData.shadowPosition = light.projection * light.view * vec4(outData.meshPosition, 1.0);
    outData.lightPosition  = light.translation.xyz;
    outData.cameraPosition = camera.translation.xyz;

    gl_Position = camera.projection * camera.view * mesh.model * position;
}