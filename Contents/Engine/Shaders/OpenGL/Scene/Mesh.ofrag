#version 460

#extension GL_ARB_shading_language_include : require

#include "../../Common/Shadow.glsl"

layout(location = 0) out vec4 outColor;

layout(location = 0) uniform sampler2DArray textures;

layout(binding = 3) uniform sampler2D shadowMap;

layout(location = 0) in Data {
    flat int textureIndex;
    vec2 UV;
    vec3 normal;
    vec4 color;
    vec3 meshPosition; 
    vec4 shadowPosition; 
    vec3 lightPosition; 
    vec3 cameraPosition; 
} inData;

void main()
{           
    vec3 color = texture(textures, vec3(inData.UV, inData.textureIndex)).rgb;
    vec3 normal = normalize(inData.normal);
    vec3 lightColor = vec3(0.3);

    vec3 ambient = 0.3 * lightColor;

    vec3 lightDirection   = normalize(inData.lightPosition - inData.meshPosition);
    vec3 viewDirection    = normalize(inData.cameraPosition - inData.meshPosition);
    vec3 reflectDirection = reflect(-lightDirection, normal);
    vec3 halfwayDirection = normalize(lightDirection + viewDirection);

    vec3 diffuse  = max(dot(lightDirection, normal), 0.0) * lightColor;
    vec3 specular = pow(max(dot(normal, halfwayDirection), 0.0), 64.0) * lightColor;   

    float shadow = ShadowCalculation(shadowMap, inData.shadowPosition, normalize(inData.normal), lightDirection);

    vec3 lighting = (ambient + (1.0 - shadow) * (diffuse + specular)) * color;    

    outColor = vec4(lighting, 1.0);
}